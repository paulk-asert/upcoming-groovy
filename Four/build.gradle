repositories {
    mavenCentral()
//    mavenLocal()
}

dependencies {
    implementation "org.apache.groovy:groovy-all:4.0.2"
    implementation "org.apache.groovy:groovy-contracts:4.0.2"
    implementation "org.apache.groovy:groovy-toml:4.0.2"
    implementation "org.apache.groovy:groovy-ginq:4.0.2"
    implementation "org.apache.groovy:groovy-macro-library:4.0.2"
    implementation "org.apache.groovy:groovy-typecheckers:4.0.2"
    implementation "net.jqwik:jqwik:1.6.5"
//    runtime "commons-cli:commons-cli:1.4"
}

task runScripts {
    description = 'run all scripts in src/main/resources'
    doLast {
        file('src/main/resources').eachFile { script ->
            if (script.name.contains('Snippet')
                    || (script.name.contains('JDK17') && !JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17))) {
                ant.echo "Skipping $script"
            } else {
                ant.echo "Executing $script"
                javaexec {
                    classpath = sourceSets.main.runtimeClasspath
                    main = 'groovy.ui.GroovyMain'
                    args = [script.path]
                }
            }
        }
    }
}

task console(type: JavaExec) {
    main = 'groovy.console.ui.Console'
    classpath = sourceSets.main.runtimeClasspath
}
